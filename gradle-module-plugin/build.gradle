
plugins {
    id "java-gradle-plugin"
    id "com.gradle.plugin-publish" version "0.12.0"
    id "org.jetbrains.kotlin.jvm" version "1.4.31"
    id 'org.jetbrains.kotlin.kapt' version "1.4.31"
    id "com.diffplug.spotless" version "5.12.2"
    id "maven-publish"
}

ext {
    slf4j_version = "1.7.30"
}

repositories {
    mavenLocal()
    maven {
        url 'https://nexus.inductiveautomation.com/repository/public'
    }
    mavenCentral()

}

group = "io.ia.sdk"
version = "0.1.0-SNAPSHOT-13"

dependencies {
    // Align versions of all Kotlin components
    implementation platform('org.jetbrains.kotlin:kotlin-bom')
    // Use the Kotlin JDK standard library.
    implementation('org.jetbrains.kotlin:kotlin-stdlib')
    implementation('org.jetbrains.kotlin:kotlin-reflect')
    implementation("org.redundent:kotlin-xml-builder:1.7.2")
    implementation("com.squareup.moshi:moshi-kotlin:1.9.3")
    kapt("com.squareup.moshi:moshi-kotlin-codegen:1.9.3")
    implementation("com.google.guava:guava:30.1.1-jre")
    api('com.inductiveautomation.ignitionsdk:module-signer:0.0.1.ia')

    // Use the Kotlin test library.
    testImplementation('org.jetbrains.kotlin:kotlin-test')
    // Use the Kotlin JUnit integration.
    testImplementation('org.jetbrains.kotlin:kotlin-test-junit')
    testImplementation("io.ia.sdk.tools.module.gen:generator-core")
}

gradlePlugin {
    plugins {
        modl {
            id = "io.ia.sdk.modl"
            implementationClass = "io.ia.sdk.gradle.modl.IgnitionModlPlugin"
        }
    }
}

pluginBundle {
    website = "https://www.github.com/inductiveautomation/ignition-module-tools"
    vcsUrl = "https://github.com/inductiveautomation/ignition-module-tools"
    description = "Create Modules to add capabilities to Inductive Automation's Ignition platform."
    plugins{
        modl {
            displayName = "Ignition Module Builder Plugin"
            tags = ["inductiveautomation", "inductive automation", "ignition", "module", "modl", "maker", "iiot"]
            version = project.version
        }
    }

    mavenCoordinates {
        // coordinates are automatically established as `gradle.plugin.${project.group}` when publishing to
        // gradle plugin portal, so no need to set this here.
        // groupId = project.getGroup()
        artifactId = "gradle-module-plugin"
        version = project.version
    }
}

jar {
    manifest {
        attributes 'Implementation-Title': project.name,
                'Implementation-Version': project.version,
                'Built-By': System.getProperty('user.name'),
                'Built-JDK': System.getProperty('java.version'),
                'Built-Gradle': gradle.gradleVersion//,
                // 'Build-Time': Calendar.getInstance().getTime(),
                // 'commit-sha1':
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

compileKotlin {
    kotlinOptions{
        jvmTarget = '11'
        // will retain parameter names for java reflection
        javaParameters = true
    }
}

compileTestKotlin {
    kotlinOptions{
        jvmTarget = '11'
        javaParameters = true
    }
}

// Add a source set for the functional test suite
sourceSets {
    functionalTest {
        compileClasspath += sourceSets.main.output
        compileClasspath += configurations.testRuntimeClasspath
        compileClasspath += configurations.runtimeClasspath
        runtimeClasspath += sourceSets.main.output
    }
}

// Add a task to run the functional tests
task functionalTest(type: Test) {
    testClassesDirs = sourceSets.functionalTest.output.classesDirs
    classpath = sourceSets.functionalTest.runtimeClasspath
}

gradlePlugin.testSourceSets(sourceSets.functionalTest)
configurations.functionalTestImplementation.extendsFrom(configurations.testImplementation)

check.dependsOn(tasks.functionalTest)
test.dependsOn(tasks.functionalTest)

spotless {
    kotlin {
        ktlint("0.39.0").userData(['max_line_length': "120"])
    }
}
wrapper {
    gradleVersion "7.0"
    distributionType org.gradle.api.tasks.wrapper.Wrapper.DistributionType.ALL
}
